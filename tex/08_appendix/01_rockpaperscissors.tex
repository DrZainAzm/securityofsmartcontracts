\section{Rock-Paper-Scissors with Correct Incentives}
\label{appendix:rpsincentivescorrected}

In section \ref{section:vulnerability:miners:rockpaperscissor} and section \ref{section:other:incentive}, different possible mistakes in the implementation "Rock Paper Scissors"-contract were shown.


The orignal and flawed contract was posted on Etherscan at address \cite{etherscan:rockpaperscissor}. The main problem of this contract was, that entering the game as the first player, due to the blockchain being public, the second player would be able to see the move before sending the transaction and therefore can always win by selecting a winning option.

\begin{minted}{solidity}
struct Hand {
    uint hand;
}

mapping(address => Hand[]) tickets;

function Rock(){ setHand(uint(1)); }
function Paper(){ setHand(uint(2)); }
function Scissors(){ setHand(uint(3)); }
\end{minted}

When trying to solve this problem using a \textit{commit-reveal-scheme}, it has to be made sure that the incentives of the players are set right, namely, that if a player knows they lose, they don't have an advantage by not revealing and therefore blocking the contract.

Additionally, in this version  the contract occupies unnecessary space, because ticket keeps all moves ever taken in the history of the smart contract.

Delmolino et al. provided an implementation (\cite{delmolino:rps}) of a commit-reveal-scheme in Serpent, but their solution incentivizes the second player to keep the contract stuck if the first player had revealed a move winning -- which they pointed out in the discussion, but did not attempt to fix.

Because of this, a secure implementation of a commit-reveal scheme is presented:
\begin{minted}{solidity}
pragma solidity ^0.4.24;

contract RockPaperScissors {
    enum Move {
        Rock,
        Paper,
        Scissors,
        NotInGame,
        NotRevealedYet
    }
    
    struct Player {
        bytes32 commitment;
        address paymentAddress;
        Move move;
    }
    
    Player playerOne = Player({
        move: Move.NotInGame,
        paymentAddress: address(0),
        commitment: bytes32(0)
    });
    Player playerTwo = Player({
        move: Move.NotInGame,
        paymentAddress: address(0),
        commitment: bytes32(0)
    });

    uint constant commitment = 1 ether;
    uint constant fee = 100 finney;
    uint constant deposit = 1 ether;

    uint lastChange;
    uint constant timeoutBlocks =  1 * 60 * 5; // 1 hour in blocks
    
    address owner;
    
    mapping(address => uint) public balances;
    
    modifier onlyAfterTimeout() {
        require(block.number >= lastChange + timeoutBlocks);
        _;
    }
    
    modifier resettingTimeout() {
        _;
        lastChange = block.number;
    }

    constructor () public {
        owner = msg.sender;
    }
    
    function commit(bytes32 inputCommitment) public payable resettingTimeout {
        uint senderAccountBalance = msg.value + balances[msg.sender];

        require(senderAccountBalance >= commitment + deposit);
        
        senderAccountBalance -= deposit + commitment;
        balances[msg.sender] = senderAccountBalance;
        
        // Select Player with smaller number, that is not in the game
        Player storage currentPlayer = playerTwo;
        if (playerOne.move == Move.NotInGame) {
            currentPlayer = playerOne;
        } else if(playerTwo.move != Move.NotInGame
            || playerOne.paymentAddress == msg.sender) {
            revert();
        }
        
        currentPlayer.move = Move.NotRevealedYet;
        currentPlayer.paymentAddress = msg.sender;
        currentPlayer.commitment = inputCommitment;
    }
    
    function hasPlayerWon(Move player, Move opponent) internal pure returns (bool) {
        return (player == Move.Scissors && opponent == Move.Paper) ||
            (player == Move.Paper && opponent == Move.Rock) ||
            (player == Move.Rock && opponent == Move.Scissors);
    }    

    // [Entry condition] Both players have revealed,
    // or both players have committed and time has run out.
    function resolve() internal {
        uint newPlayerOneBalance = balances[playerOne.paymentAddress];
        uint newPlayerTwoBalance = balances[playerTwo.paymentAddress];
        uint newOwnerBalance = balances[owner];
        
        if(playerOne.move >= Move.NotInGame && playerTwo.move >= Move.NotInGame) {
            // If both players didn't reveal, keep all the deposits and commitments
            newOwnerBalance += 2 * commitment + 2 * deposit;
        } else if(playerOne.move >= Move.NotInGame) {
            // If one player didn't reveal, treat other player as if he had won,
            // and keep deposit
            newPlayerTwoBalance += 2 * commitment + deposit - fee;
            newOwnerBalance += fee + deposit;
        } else if(playerTwo.move >= Move.NotInGame) {
            newPlayerOneBalance += 2 * commitment + deposit - fee;
            newOwnerBalance += fee + deposit;
        } else if(hasPlayerWon(playerOne.move, playerTwo.move)) {
            newPlayerOneBalance += 2 * commitment + deposit - fee;
            newPlayerTwoBalance += deposit;
            newOwnerBalance += fee;
        } else if (hasPlayerWon(playerTwo.move, playerOne.move)) {
            newPlayerTwoBalance += 2 * commitment + deposit - fee;
            newPlayerOneBalance += deposit;
            newOwnerBalance += fee;
        } else {
            newPlayerTwoBalance += commitment + deposit;
            newPlayerOneBalance += commitment + deposit;
        }
        
        balances[playerOne.paymentAddress] = newPlayerOneBalance;
        balances[playerTwo.paymentAddress] = newPlayerTwoBalance;
        balances[owner] = newOwnerBalance;

        playerOne.move = Move.NotInGame;
        playerTwo.move = Move.NotInGame;
    }
    
    function hasRevealed(Move move) internal pure returns(bool) {
        return move < Move.NotInGame;
    }
    
    function isCorrectMove(Move move) internal pure returns(bool) {
        return move == Move.Rock || move == Move.Scissors || move == Move.Paper;
    }  uint withdrawerBalance = balances[msg.sender];
        
    require(withdrawerBalance > 0);
   
    
    // Player can reveal if other player has not committed yet,
    // but only makes himself vulnerable by doing that
    function reveal(Move move, string salt) public resettingTimeout() {
        Player storage currentPlayer = playerTwo;
        
        if(playerOne.paymentAddress == msg.sender && playerOne.move == Move.NotRevealedYet) {
            currentPlayer = playerOne;
        } else if(!(playerTwo.paymentAddress == msg.sender && playerTwo.move == Move.NotRevealedYet)) {
            revert();
        }
        
        require(keccak256(abi.encodePacked(move, salt)) == currentPlayer.commitment);
        require(isCorrectMove(move));
        currentPlayer.move = move;
        
        if(hasRevealed(playerOne.move) && hasRevealed(playerTwo.move)) {
            resolve();
        }
    }
    
    function kickUnrevealed() onlyAfterTimeout resettingTimeout public {
        require(playerOne.move != Move.NotInGame && playerTwo.move != Move.NotInGame);
        
        resolve();
    }
    
    function withdraw () public {
        uint withdrawerBalance = balances[msg.sender];
        
        require(withdrawerBalance > 0);
        
        balances[msg.sender] = 0;
        msg.sender.transfer(withdrawerBalance);
    }
}
\end{minted}

A correct game of "Rock Paper Scissors" using this contract could contain the following interactions:

\begin{itemize}
	\item Player 1 commits to scissors by sending the hash of \mintinline{solidity}{Move.Scissors} and the salt string \mintinline{solidity}{"saltoffirstplayer"} to commit; sending the required two Ether along with the message:
	      \begin{minted}{solidity}
commit(0xbe141094275f044dc3c34c5b50b1ada4007925debed74bfc7071b518840b5203)
\end{minted}
	\item Afterwards, Player 2 enters the game by making his move \mintinline{solidity}{Move.Rock} hashed along with the "random" salt \mintinline{solidity}{"saltofsecondplayer"} to commit:
	      \begin{minted}{solidity}
commit(0x96b9d5fad3ef05f587c11b52f41e6d4f215ef9ff0397b98a7dd5b5de5e151843)
\end{minted}
	\item Player 2 directly reveals afterwards by sending in their move:
	      \begin{minted}{solidity}
reveal(Move.Rock, "saltofsecondplayer")
\end{minted}
	\item Player 1 follows by sending in
	      \begin{minted}{solidity}
reveal(Move.Scissors, "saltoffirstplayer")
\end{minted}
\end{itemize}

Because of the required deposit, Player 2 was strongly incentivized to reveal although they had already know they lost, because otherwise the deposit would have been kept by the owner of the contract.

The result of this execution is a balance of 1 Ether for Player 1, and a balance of 3 Ether for player two who has won the game, that later can be withdrawn using \mintinline{solidity}{withdraw()}.

\pagebreak{}
